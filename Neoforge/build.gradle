plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.109'
}

version = version
group = group

repositories {
    mavenLocal()
}

base {
    archivesName = "${project.mod_name}-neoforge-${minecraft_version}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

runs {
    configureEach {

        systemProperty 'neoforge.logging.markers', 'REGISTRIES'
        systemProperty 'neoforge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {

    }

    server {
        programArgument '--nogui'
    }
}

// ignore neo's compile tasks, else it compiles our common classes into the base game
Spec<Task> notNeoTask = { Task it -> !it.name.startsWith("neo") } as Spec<Task>

tasks.withType(JavaCompile).matching(notNeoTask).configureEach {
    source(project(":Common").sourceSets.main.allSource)
}

processResources {
    from project(":Common").sourceSets.main.resources
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"
    compileOnly project(":Common")
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
